// Top-level build file of Java projects.

// I don't know why but junit can't be imported with `plugins` block as
// other plugins.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    id 'java'
    id 'net.ltgt.apt' version '0.10'
}

allprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        jcenter()
    }

    // These are global dependencies, where you should only add the bare
    // minimum stuff.
    dependencies {
        compile 'com.google.dagger:dagger:2.10'
        apt     'com.google.dagger:dagger-compiler:2.10'

        compile 'args4j:args4j:2.33'
        compile 'com.google.code.findbugs:jsr305:3.0.2'
        compile 'com.google.guava:guava:21.0'
        compile 'org.slf4j:slf4j-api:1.7.25'

        // Let's use slf4j-simple framework for now and (maybe) switch to a
        // more sophisticated framework later.
        compile 'org.slf4j:slf4j-simple:1.7.25'

        testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M4'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M4'
    }

    shadowJar {
        // Don't know what are these - just remove them.
        exclude 'META-INF/maven/**'
        exclude 'OSGI-OPT/**'
    }

    // Only "leaf" projects should produce shadow JAR.
    shadowJar.enabled = false;

    // Configure Intellij IDEA to locate generated code.
    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs +=
                file(new File(buildDir, 'generated/source/apt/main'))
        }
    }
}

// It is silly to track the gradle wrapper in the version control.
// Run `gradle wrapper` to generate the wrapper instead.
task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
